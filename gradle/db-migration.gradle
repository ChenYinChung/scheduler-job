buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.flywaydb:flyway-gradle-plugin:6.0.0-beta2'
        classpath files("${rootDir}/libs/postgresql-42.2.2.jar")
    }
}

bootJar{
    enabled = false
}

// use gradlew -Penv=${env} to pass
def env = hasProperty('env') ? env : "local"

apply plugin: org.flywaydb.gradle.FlywayPlugin

tasks.withType(org.flywaydb.gradle.task.AbstractFlywayTask.class) { task ->
    task.doFirst {
        flyway {
            placeholderReplacement = true
            outOfOrder = true
            
            if(env.contains("local") || env.contains("sit")){
                cleanDisabled = false
            }else{
                cleanDisabled = true
            }

            assert project.file('src/main/resources/db/migration').exists()
            def flywayProperties = "conf/${env}/flyway.properties"
            assert project.file(flywayProperties).exists()

            Properties properties = new Properties()
            project.file(flywayProperties).withInputStream {
                properties.load(it)
            }
            properties.each { key, value ->
                project.ext[key as String] = value
            }
        }
    }
}